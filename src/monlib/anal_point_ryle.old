C+ANAL_POINT_RYLE

       subroutine anal_point_Ryle (lsf_num, plot_device, status)
C      ---------------------------------------------------------
C
C Analyse 5-point and raster offset observations
C
C  Given:
C      LSF_NUM       integer       logical sample file number
C      plot_device   char*(*)      PGplot device
C      STATUS        integer       status value
C
C  Scans the given logical sample file, for a pointing observation,
C  merging visibilities for each spacing (i.e. over sub-band/channel),
C  and displaying an analysis of the observed visibilities by pointing
C  offset.
C
C  The STATUS value should be zero on entry.
C
*-

* 15 Sept 93

       integer        lsf_num, status
       character*(*)  plot_device


       include '/mrao/post/include/global_constants.inc'
       include '/mrao/post/include/samplib_errors.inc'
       include '/mrao/post/include/phys_tscopes.inc'
       include '/mrao/post/include/merge_types.inc'
       include '/mrao/post/include/offset_types.inc'
       include '/mrao/post/include/ryle_point_anal.inc'
       include '/mrao/include/iolib_functions.inc'
       include '/mrao/include/iolib_errors.inc'
       include '/mrao/include/constants.inc'

       character     title(4)*80
       character     list*80, source*80, text*8
       character*64  file, out_file, heading(4)
       character*1   offset_list(8)
       character*3   default
       character*1   ftype
       character*5   access(3)
       character*8   ae_list

       integer    ibuff, ibuff1, ibuff2, isamp, sf_lun, src_num, tscope
       integer    i, ii, iae1, iae2, isp, iba, ich, j, k
       integer    u, v, ix, iy, archive, iobjx, blockwords
       integer    iold, iout, termi, termo
       integer*4  npages, nbytes, dates(3)
       integer    merge_type
       integer    aerial, aerial1, aerial2
       integer    nspac, no_groups
       real       offset_angle
       integer    offset_type, offset_time
       integer    offset_aerial(8), offset_ae_num, offset_plot_num
       integer    offset2Nx, offset2Ny, offset_zero
       integer    ha_5point(0:4), dec_5point(0:4)
       integer    sm_type, sm_size, sm_rate, sid, i20
       integer    W_offset, E_offset, N_offset, S_offset, no_offset
       integer    n, nx, ny, wx, wy
       integer    date1(3), time1(3), date2(3), time2(3)

       real       rcos, rsin, ramp, rphi, ha, r20
       real       ratio, sign, value
       real       W_amp, E_amp, N_amp, S_amp, on_amp
       real       WE_ratio, NS_ratio, WE_error, NS_error
       real       x, y, devx, devy
       real       sx, sy, sxx, syy, sh_x, shh_x, sh_y, shh_y, sxh, syh
       real       m_ha, c_ha,  m_dec, c_dec
       real       rfreq
       real*8     epoch, radate, decdate, freq

       integer    sp_list(max_vis)
       integer    merge_list(2*max_vis), group_size(2*max_vis)

       logical    Lprint
       complex    vis_list(max_vis), vis_merge(max_vis)

       real       hour_angle(max_samp), amplitude(max_samp)
       real       ha_point(max_samp),   dec_point(max_samp)
       real       mean_ha(max_samp)
       complex    visibility(max_samp)
       integer*2  ha_weight(max_samp), dec_weight(max_samp)


       integer    bytesperword
       parameter (bytesperword = 4)
       real       pi, min_amp
       parameter (pi = const_pi, min_amp = 0.05)


* table of offset angle af function of A/A', the ratio of amplitudes
* for offsets +- half voltage point of one aerial in a pair

* tabulated in arcseconds for A/A' = 1.00:0.05:3.00
* data from SK's table, last page of log book, volume 2
* [these values appropriate to 5 GHz: taken to scale as 1/f]


      integer point_error(0:40)/
     #      0,  9, 19, 28, 37, 45, 53, 61, 69, 76, 81,
     #         87, 93, 98,104,110,115,120,125,130,134,
     #        138,143,147,152,157,161,166,170,175,179,
     #        184,188,192,196,200,203,207,210,214,218/

      integer off_scale
      parameter (off_scale = 240)

* function
      integer Wfile

*  Place work arrays in the work space common block
       common  /post/  sp_list, merge_list, group_size,
     :                 vis_list, vis_merge,
     :                 hour_angle, amplitude,
     :                 ha_point, dec_point, mean_ha


       if (status.ne.0) return

C   current output unit
       call io_enqtio(termi,termo)
       call io_enqout(iout)
       iold = iout

C   sample file info
       call lsf_enq_sf(lsf_num, sf_lun, src_num, status)

C  Check telescope type
       call enq_phys_tscope(sf_lun, tscope, status)
       if (tscope.ne.RYLE) status = ILL_TSCOPE

      call enq_pc_epoch(sf_lun,1,epoch,radate,decdate,source,status)
      call enq_freq(sf_lun, freq, status)

C  Set merge type:
C   'subband' merges visibilities over channels and sub-bands for each
C      spacing, providing one output visibility for each spacing.
C   'channel' merges visibilities over channels for each sub-band,
C      providing one output visibility for each sub-band/spacing.

*      merge_type = subband_merge
       merge_type = channel_merge

* type of offset, aerials

      call enq_off_tables (sf_lun, offset_type, offset_aerial,
     #                     offset_angle, offset_time,
     #                     ha_5point, dec_5point,
     #                     offset2Nx, offset2Ny,
     #                     offset_zero, status)

      if (offset_type .ne. o_5point) then
          write (iout, *) 'not a 5-point offset'
          goto 9010
      endif

      text = '        '
      offset_ae_num = 0
      do j = 1,8
          if (offset_aerial(j) .eq. 1) then
              text(j:j) = char(ichar('0')+j)
              offset_ae_num = offset_ae_num + 1
              offset_list(offset_ae_num) = text(j:j)
          endif
      enddo
      offset_plot_num = 1

      if (offset_ae_num .eq. 0) then
          write (iout, *) 'no aerials offset??'
      else
          write (iout, *) ' aerials offset are: ', text
      endif
      write (iout, 1030) ' offset angle', offset_angle,' min arc; ',
     #                   offset_time, ' samples/point'
 1030 format(1x, A, f5.1, A, i3, A)

* find which elements correspond to which offsets

      W_offset = -1
      E_offset = -1
      N_offset = -1
      S_offset = -1
      no_offset= -1

      do i = 0, 4
          if ( ha_5point(i) .eq.  1)  W_offset = i
          if ( ha_5point(i) .eq. -1)  E_offset = i
          if (dec_5point(i) .eq.  1)  N_offset = i
          if (dec_5point(i) .eq. -1)  S_offset = i
          if ( ha_5point(i) .eq. 0 .and. dec_5point(i) .eq. 0)
     #                               no_offset = i
      enddo

      if    (W_offset .lt. 0 .or. E_offset .lt. 0
     # .or.  N_offset .lt. 0 .or. S_offset .lt. 0) then
          write (iout, *) 'incomplete offset table'
          goto 9010
      endif

      write (iout, '(1x, A, 5i2)')' offset samples (no, W, E, N, S) :',
     %       no_offset, W_offset, E_offset, N_offset, S_offset

*check sampling

      call LSF_enq_smooth (lsf_num, sm_type, sm_size, sm_rate, status)
      write (iout, 1002) sm_type, sm_size, sm_rate
 1002 format(1x, ' smooth type',i2,'  length',i3,'  sample-rate',i3)

      if (offset_time .ne. sm_size  .or.
     #    offset_time .ne. sm_rate) then
*          call fix_smooth_rate(offset_time, status)
*          write (iout, 1001) offset_time
          write (iout, *) 'set smoothing and sampling to ',offset_time
          goto 9010
      endif

*1001 format (1x, 'smoothing and sampling reset to', i2)


C  Get range of sample buffers
       call lsf_get_range(lsf_num, ibuff1, ibuff2, status)
       ibuff2 = (ibuff2/5)*5      ! whole no of cycles

       if (status.eq.0) then

              if  (ibuff2-ibuff1 .lt. 5)   then
                  write (iout, *) 'less than one offset cycle'
                  goto 9010
              endif
      endif

* set up data for the archive file


      do j = 0, max_point
          pointing(j) = 0
      enddo

* time, date, ra4date, dec4date, wind-speed statistics, source

      call enq_loc_time (sf_lun, date1, time1, date2, time2, status)
      do j = 1, 3
          date(j) = date1(j)
          time(j) = time1(j)
      enddo
      ra4date     = radate
      dec4date    = decdate
      obs_freq    = freq
      o_angle     = offset_angle
      source_name = source
      comment     = ' '

* ha1, ha2 filled in later

* wind-speed statistics

       n=0
       mean_wind =    0.0
       max_wind  = -100.0
       min_wind  =  100.0
       do isamp = (ibuff1-1)*offset_time+1, (ibuff2-1)*offset_time+1
         call set_monitor(sf_lun,isamp,status)
         call enq_mon_wind(sf_lun,value,status )

         if (value.lt.min_wind) then
           min_wind = value
         elseif (value.gt.max_wind) then
           max_wind = value
         endif
         mean_wind = mean_wind + value
         n=n+1

       enddo

       mean_wind = mean_wind/n

       Lprint = io_yesno('print the raw data ?', 'no', status)

       call pgbegin(0, plot_device, 1, 1)

C  output file
       call io_opeout(iout, status)
       if (status.eq.0) then
         if (iout.ne.termo)  write(iout,*) '5-point analysis'
         write(iout,*)
         inquire (unit=sf_lun, name=file)
         call io_lstfil(iout, file, status)
       endif

       call lsf_title(lsf_num, list, ibuff1, ibuff2, title, status)
       write(iout,'(x,a)')title
       write (iout, *)
       write (iout, 1050) mean_wind, min_wind, max_wind
1050   format (1x, 'Wind-speed statistics: ',
     *    '  mean :', f6.1, '  min :', f6.1, '  max:', f6.1,' knots')
       write (iout, *)
       write (iout, *) 'error/arcsec = m*H + c',
     #                 '  [H as fraction of pi]',
     #                 '  > 0 if aerial points too far E/S'
       write (iout, *)
       write (iout, *)    'ae     spacing         ',
     %                    'm(ha)  c(ha)        fit       ',
     %                    'm(dec)  c(dec)        fit'
      write (iout, *)

C   list of spacings

  500  call get_spacings(sf_lun, 'spacings : ',
     :                     'ae'//offset_list(offset_plot_num),
     :                     list, sp_list, max_vis, nspac, status)
       call lsf_set_spacings(lsf_num, nspac, sp_list, 2, status)
       call chr_chucas(list)
       if (status .ne. 0) goto 9000
      call lsf_enq_ae_vis(lsf_num, 1, aerial1, aerial2, status)
      write (iold, '(1x, A, 2i2)') 'aerials', aerial1, aerial2
      do  i = 1, nspac
          call lsf_enq_ae_vis(lsf_num, i, iae1, iae2, status)
          if (status .ne. 0) goto 9000
          if (aerial1 .ne. iae1 .or. aerial2 .ne. iae2) then
              write (iold, *) 'More than one aerial pair included'
              goto 500
          endif
      enddo

* make sure that only one aerial is offset

      if      (offset_aerial(aerial1) .eq. 1
     # .and.   offset_aerial(aerial2) .eq. 0) then
          aerial = aerial1
      else if (offset_aerial(aerial2) .eq. 1
     # .and.   offset_aerial(aerial1) .eq. 0) then
          aerial = aerial2
      else
          write (iold, 1100)  aerial1,aerial2,text
 1100 format (1x, 'unsuitable spacing: ',i1,'/',i1,
     #            '  offset aerials are ', A)
          goto 500
      endif
      o_aerial(aerial) = 1

C  Define merge list using the specified merge-type
       call set_merge(sf_lun, sp_list, nspac, merge_type,
     :                       merge_list, no_groups, group_size, status)

      write (iold,       '(1x,A,i1)') '5-point analysis - ae ', aerial
      write (heading(1), '(1x,A,i1)') '5-point analysis - ae ', aerial
      write (heading(3), '(1x,A,2i5)')'LSF buffer range ',ibuff1,ibuff2

C  Scan the sample file
         n = 0
         do ibuff = ibuff1, ibuff2
           call lsf_set_buffer(lsf_num, ibuff, status)
           call lsf_get_vis(lsf_num, max_vis, vis_list, nspac, status)
           call merge_vis_buffer(vis_list, nspac,
     :                           merge_list, no_groups, group_size,
     :                           vis_merge, status)
           if (status.eq.0) then
              if (no_groups .ne. 1) then
                  write (iold, *) 'too many samples after merging'
                  write (iold, *) 'no_groups =', no_groups
                  goto 9000
              endif
           else
              goto 9000
           endif

* find HA and amplitude of each merged visibility

               call lsf_get_sid(lsf_num, sid, status)
               ha = 2.0*pi*sid/864000.0 - radate
               if (ha .gt. pi) ha = ha - 2*pi
               if (ha .lt.-pi) ha = ha + 2*pi
               hour_angle(ibuff) = ha
               amplitude (ibuff) = cabs(vis_merge(1))
               visibility(ibuff) = vis_merge(1)

         enddo

          ha1 = hour_angle(ibuff1)
          ha2 = hour_angle(ibuff2)


      if (Lprint) then
          write (iout, *)
          write (iout,       '(1x,A,i1)') ' data for aerial ', aerial
          write (iout, *)
     #     '          W/E   P(h)    N/S   P(d)',
     #     '      on    W     E     N     S       HA'
      endif

          j = ((ibuff1+3)/5)*5 + 1    ! round up suitably
          k = ((ibuff2-1)/5)*5 + 1    ! round down

          n = 0
          do i = j, k, 5
              n = n + 1
              W_amp = amplitude(i+W_offset)
              E_amp = amplitude(i+E_offset)
              N_amp = amplitude(i+N_offset)
              S_amp = amplitude(i+S_offset)
             on_amp = amplitude(i+no_offset)

                  if (W_amp .lt. min_amp .or. E_amp .lt. min_amp) then
                      WE_ratio = 1.0
                      ha_weight(n) = 0
                  else
                      WE_ratio = W_amp/E_amp
                      ha_weight(n) = 1
                  endif
                  if (N_amp .lt. min_amp .or. S_amp .lt. min_amp) then
                      NS_ratio = 1.0
                      dec_weight(n) = 0
                  else
                      NS_ratio = N_amp/S_amp
                      dec_weight(n) = 1
                  endif

* look up error in the table
*
*     if WE_ratio > 1.0 the aerial is pointing too far E
*     if NS_ratio > 1.0 the aerial is pointing too far S
*     both gave +ve output in SK's system

              if (WE_ratio .ge. 1.0) then
                  ratio = WE_ratio
                  sign = +1.0
              else
                  ratio = 1/WE_ratio
                  sign = -1.0
              endif

              if (ratio .gt. 3.0) then
                  WE_error = off_scale      ! off scale; nominal value
              else
                  r20 = (ratio - 1.0)*20.0
                  i20 = r20
                  WE_error = point_error(i20) +
     &              (point_error(i20+1)-point_error(i20))*(r20-i20)
              endif
              ha_point(n) = WE_error*sign*4995.0e6/freq


              if (NS_ratio .ge. 1.0) then
                  ratio = NS_ratio
                  sign = +1.0
              else
                  ratio = 1/NS_ratio
                  sign = -1.0
              endif

              if (ratio .gt. 3.0) then
                  NS_error = off_scale      ! off scale; nominal value
              else
                  r20 = (ratio - 1.0)*20.0
                  i20 = r20
                  NS_error = point_error(i20) +
     &              (point_error(i20+1)-point_error(i20))*(r20-i20)
              endif

              dec_point(n) = NS_error*sign*4995.0e6/freq
              mean_ha(n)   = hour_angle(i+3)/const_h2r    ! hours

          if (Lprint) then

             write (iout, 2010) n, i,
     #                          WE_ratio, ha_point(n),
     #                          NS_ratio,dec_point(n),
     #                          on_amp, W_amp, E_amp, N_amp, S_amp,
     #                          mean_ha(n)

 2010 format (1x, i2, i4, 2x, f6.3, f6.0, 2x, f6.3, f6.0,
     >            2x, 5f6.2, 3x, f6.3)
          endif

          enddo



C
       if (status.eq.0) then

* analysis



         ii = 1
         do i = 1, no_groups
          if (i.eq.1) write(iout,*)
          call enq_vis_desig(sf_lun, sp_list(ii), isp, iba, ich, status)
          call enq_ae_vis(sf_lun, sp_list(ii), iae1, iae2, status)
          if (merge_type.eq.channel_merge) then
*             write (iout,       1091)   isp,iae1,iae2,char(iba+64)
              write (heading(2), 1091)   isp,iae1,iae2,char(iba+64)
 1091 format (1x, 'spacing :',i4,'  aerials: ',i1,',',i1,'  sb: ',A1)
     :
          elseif (merge_type.eq.subband_merge) then
*             write (iout,       1092)   isp,iae1,iae2
              write (heading(2), 1092)   isp,iae1,iae2
 1092 format (1x, 'spacing :',i4,'  aerials: ',i1,',',i1)
          endif

          if (io_attn(status)) goto 9000
          ii = ii + group_size(i)

         enddo

* pointing results:
* =================

* least-squares fits:  initialise sums


* here x = ha pointing error, y = dec pointing error, h = hour angle
* sxh = sum of x*h etc
* sh_x, shh_x and sh_y, shh_y to allow for possible different weights

      sxx   = 0.0
      syy   = 0.0
      sxh   = 0.0
      syh   = 0.0

      sx    = 0.0
      sy    = 0.0
      sh_x  = 0.0
      shh_x = 0.0
      sh_y  = 0.0
      shh_y = 0.0
      nx    = 0
      ny    = 0

* least-sq fit : x[y] = m*h + c

* m = {n*sxh - sh*sx}/{n*shh - sh*sh}
* c = {sx*shh - sxh*sh}/{n*shh - sh*sh}




      do i = 1, n

          x  =  ha_point(i)
          y  = dec_point(i)
          ha = mean_ha(i)/12.0    ! need here HA as fr of pi
          wx =  ha_weight(i)
          wy = dec_weight(i)

          sh_x  = sh_x  + ha*wx
          shh_x = shh_x + ha*ha*wx*wx
          sh_y  = sh_y  + ha*wy
          shh_y = shh_y + ha*ha*wy*wy

          sx  = sx  + x*wx
          sy  = sy  + y*wy

          sxx = sxx + x*x*wx*wx
          syy = syy + y*y*wy*wy

          sxh = sxh + x*ha*wx
          syh = syh + y*ha*wy

          nx  = nx + wx
          ny  = ny + wy

      enddo

      if (abs(nx*shh_x-sh_x*sh_x) .lt. 1e-3) then
          write (iout, *) 'ill-conditioned ha data for fitting'
          m_ha = 0.0
          c_ha = 0.0
          devx = 0.0
          o_aerial(aerial) = 0        ! no valid data in archive
      else

          m_ha  = (nx*sxh - sh_x*sx)/(nx*shh_x - sh_x*sh_x)
          c_ha  = (sx*shh_x - sxh*sh_x)/(n*shh_x - sh_x*sh_x)

          devx = 0.0
          do i = 1, n
              devx = devx
     %     + abs(ha_point(i)-m_ha*mean_ha(i)/pi-c_ha)*ha_weight(i)
          enddo
          devx = devx/nx
      endif

      if (abs(ny*shh_y-sh_y*sh_y) .lt. 1e-3) then
          write (iout, *) 'ill-conditioned dec data for fitting'
          m_dec = 0.0
          c_dec = 0.0
          devy  = 0.0
          o_aerial(aerial) = 0        ! no valid data in archive
      else
          m_dec = (ny*syh - sh_y*sy)/(ny*shh_y - sh_y*sh_y)
          c_dec = (sy*shh_y - syh*sh_y)/(ny*shh_y - sh_y*sh_y)

          devy = 0.0
          do i = 1, n
              devy = devy
     %     + abs(dec_point(i)-m_dec*mean_ha(i)/pi-c_dec)*dec_weight(i)
          enddo
          devy = devy/ny

      endif

          write (iout, 2102) aerial, isp,iae1,iae2,char(iba+64),
     *                       m_ha,c_ha,devx, m_dec,c_dec,devy

 2102 format (1x, i1, 4x, i3, 3x, i1, '/', i1, A1,
     %          2(4x, 2F8.1, ' +- ', f8.1))

* prepare for archive file

      mha(aerial)    = m_ha
      cha(aerial)    = c_ha
      fitha(aerial)  = devx
      mdec(aerial)   = m_dec
      cdec(aerial)   = c_dec
      fitdec(aerial) = devy

* plotting ....


Clear graphics device, draw plots, HA followed by Dec

             call pgask(.false.)
             call pmadvance(status)

             call pgbbuf
             call plot_setmode('BRIEF', status)
             call pgvport( 0.1, 0.9, 0.45, 0.75 )
             call plot_data(n, mean_ha, ha_point, 1.0, 1.0, status)
             call pglabel(' ', 'W ... ha ... E', heading(1))
             call pgvport(0.1, 0.9, 0.1, 0.40)
             call plot_data(n, mean_ha, dec_point, 1.0, 1.0, status)
             call pglabel('Hour angle', 'N ... dec ... S', ' ')

c    title
             call pgvport( 0.1, 0.9, 0.85, 1.0 )
             call pgwindow( 0.0, 100.0, 4.2, -0.2 )
             call pgtext(0.0, 1.0, file)
             call pgtext(0.0, 2.0, heading(1))
             call pgtext(0.0, 3.0, heading(2))
             call pgtext(0.0, 4.0, heading(3))
             call pgebuf


          offset_plot_num = offset_plot_num + 1
          if (offset_plot_num .le. offset_ae_num) then
              default = 'yes'
          else
              default = 'no'
          endif
          if (io_yesno('another spacing ?', default, status)) goto 500


          if (status .eq. 0) then

c           if (io_yesno('do you want to write the data to the file ?',
c    #                'no', status))  then

* open the file; find last block; set index to next one;
* write new block; close

c               call io_nxtlun (archive, status)
c               open (archive,  file   = pointing_data,
c    +                          status = 'OLD',
c    +                          access = 'WX',
c    +                          iostat = status)
c               if (status .ne. 0) goto 9000
c               blockwords = bytesperblock/bytesperword
c               call setbs (archive, blockwords)
c               call io_enqfil (pointing_data, iobjx, npages,
c    +                      nbytes, ftype, access, dates,
c    +                      status)
c               i = nbytes/bytesperblock
c               index = i
c               status = wfile(archive, 0, pointing, i,
c    %                         bytesperblock/bytesperword)
c               close (archive)

c               ae_list = ' '
c               do j = 1, 8
c                   if (o_aerial(j) .ne. 0)
c    *                    ae_list(j:j)=char(ichar('0')+j)
c               enddo

c               write (iout, *)
c               write (iout, 3000) source_name,time(3),time(2),date,
c    &                             ae_list,i
c               write (iout, 3001) pointing_data

c3000           format (1x, A, 2x, i2.2,':',i2.2,2x,2i3.2,i5,
c    &                '  offset aerials ', A, '  block no ', i3)
c3001           format (1x, 'written to ', A)

c            endif
          endif
      endif

      goto 9000


C  Tidy up

 9000  call pgend
 9010  call io_close(iout, status)
       call io_setout(iold)
       write(iold,*)

       if (status.ne.0 .and. status.eq.USR_BREAK) then
         call smp_wrerr(status, 'in routine ANAL_POINT_RYLE')
       endif

       end


